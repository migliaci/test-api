group 'com.migliaci'
version version

apply plugin: 'groovy'
apply plugin: 'war'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/groovy')
        }
    }
}


configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    //may help me with startup tasks
    //https://github.com/eugenp/tutorials/blob/master/persistence-modules/java-cassandra/src/test/java/com/baeldung/cassandra/java/client/repository/BookRepositoryIntegrationTest.java
    compile "org.springframework:spring-web:${springVersion}"
    compile "org.springframework:spring-aop:${springVersion}"
    compile "org.springframework:spring-webmvc:${springVersion}"
    compile "org.springframework:spring-context:${springVersion}"
    compile "com.datastax.cassandra:cassandra-driver-core:${cassandraVersion}"
    compile "com.datastax.cassandra:cassandra-driver-mapping:${cassandraVersion}"
    compile "com.datastax.cassandra:cassandra-driver-extras:${cassandraVersion}"
    compile "io.springfox:springfox-swagger2:${swaggerVersion}"
    compile "io.springfox:springfox-swagger-ui:${swaggerVersion}"
    compile "com.sun.jersey:jersey-client:1.8"
    compile "org.apache.commons:commons-lang3:3.2.1"
    compile("org.codehaus.groovy:groovy-all:${groovyVersion}") {
        exclude(module: 'javax.servlet-api')
    }
    compile "org.glassfish.hk2.external:javax.inject:2.2.0"
    compile "org.apache.httpcomponents:httpclient:4.3.5"
    compile "org.objenesis:objenesis:2.2"
    compile "net.sf.ehcache:ehcache:2.9.1"
    providedCompile "javax.servlet:javax.servlet-api:3.0.1"
    compile "org.apache.httpcomponents:httpcore-nio:4.3.2"
    compile "com.fasterxml.jackson.core:jackson-databind:2.8.6"
    compile "org.cassandraunit:cassandra-unit:${cassandraUnitVersion}"
    testCompile("org.cassandraunit:cassandra-unit:${cassandraUnitVersion}") {
        exclude(module: 'jackson-core-asl')
        exclude(module: 'jackson-mapper-asl')
        exclude(module: 'cassandra-driver-core')
        exclude(module: 'guava')
        exclude(module: 'cassandra-driver-mapping')
        exclude(module: 'netty-all')
    }
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile "org.spockframework:spock-core:${spockVersion}"
    testCompile "org.spockframework:spock-spring:${spockVersion}"
    testCompile "org.springframework:spring-test:${springVersion}"
    testCompile "cglib:cglib-nodep:2.2"
    integrationTestCompile "org.cassandraunit:cassandra-unit:${cassandraUnitVersion}"
    integrationTestCompile "org.springframework:spring-test:${springVersion}"
    integrationTestCompile "org.spockframework:spock-core:${spockVersion}"
    integrationTestCompile "org.spockframework:spock-spring:${spockVersion}"

}

//A task that builds and deploys the WAR file with the version stripped off, simplifying IntelliJ deployment to Tomcat -- see README.md
task copyWarForIntellij(type:Copy, group: 'build', description: 'Utility task that strips the version for deploying the WAR through IntelliJ') {
    def newArchiveName = "${war.archiveName.replace("-${version}", '')}"
    from(war.destinationDir)
    into(war.destinationDir)
    include(war.archiveName)
    rename(war.archiveName, newArchiveName)
}
copyWarForIntellij.onlyIf { System.hasProperty('intellij-build') || project.hasProperty('intellij-build')}
copyWarForIntellij.dependsOn ':test-api-webapp:assemble'